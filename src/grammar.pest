journal = {
    SOI ~ blank_line* ~ xact_list? ~ blank_line* ~ EOI
}

xact_list = {
    xact ~ (blank_line* ~ xact)*
}

xact = {
    xact_date ~ (ws+ ~ state)? ~ (ws+ ~ code)? ~ ws+ ~ payee ~ ((ws* ~ comment) | (ws* ~ eol)) ~ postings

}

xact_date = {
    tx_date ~ ef_date?
}

tx_date = {
    date
}

ef_date = {
    "=" ~ date
}

state = {
    "*"
  | "!"
}

code = {
    "(" ~ code_text ~ ")"
}

payee = @{
    (!("(" | ")" | ";") ~ any_print)+ ~ (ws+ ~ (!("(" | ")" | ";") ~ any_print)+)*
}

comment = {
    (eol ~ ws+)? ~
    (commet_char ~ ws* ~ comment_text ~ eol) ~
    (ws+ ~ (commet_char ~ ws* ~ comment_text ~ eol))*
}


postings = {
    posting+
}

posting = {
    ws+ ~ (state ~ ws*)?  ~ account ~ ws{2,} ~ quantity ~ (ws+ ~ lots)? ~ (ws+ ~cost)?  ~ ((ws* ~ comment) | (ws* ~ eol)) |
    // ws+ ~ (state ~ ws*)?  ~ account ~ ws{2,} ~ quantity ~ ws* ~ eol |
    ws+ ~ (state ~ ws*)?  ~ account ~ ((ws* ~ comment) | (ws* ~ eol))
}

account = {
    account_name
  | "(" ~ account_name ~ ")"
  | "[" ~ account_name ~ "]"
}

account_name = @{
    account_word ~ (ws ~ account_word)*
}

account_word = @{
    (!('0'..'9' | "." | "," | "/" | "@" | " " | "\t" | "(" | ")" | ";" | "!" | "*") ~ any_on_line)+
}

quantity = {
    units_value // | units_expr
}

units_value = {
    ammount ~ ws* ~commodity
  | commodity ~ ws* ~ammount
}


commodity = @{
    (!(ws | ASCII_DIGIT  | "." | "," | ";" | ":" | "?" | "!" | "-" | "+" | "*" | "/" | "^" | "&" | "|" | "=" | "{" | "}" | "[" | "]" | "<" | ">" | "(" | ")" | "@") ~ any_on_line)+
  | "\"" ~ text ~ "\""
}

lots = {
    lot_price ~ ws* ~lot_date  ~ ws* ~lot_note
  | lot_price ~ ws* ~lot_note  ~ ws* ~lot_date
  | lot_date  ~ ws* ~lot_price ~ ws* ~lot_note
  | lot_date  ~ ws* ~lot_note  ~ ws* ~lot_price
  | lot_note  ~ ws* ~lot_price ~ ws* ~lot_date
  | lot_note  ~ ws* ~lot_date  ~ ws* ~lot_price
  | lot_price ~ ws* ~lot_date
  | lot_price ~ ws* ~lot_note
  | lot_date  ~ ws* ~lot_price
  | lot_date  ~ ws* ~lot_note
  | lot_note  ~ ws* ~lot_price
  | lot_note  ~ ws* ~lot_date
  | lot_price
  | lot_date
  | lot_note
}

lot_price = {
    "{" ~ point_value ~ "}"
  | "{=" ~ fixing_value ~ "}"
}

point_value = {
    units_value
}

fixing_value = {
    units_value
}

lot_date = {
    "[" ~ date ~ "]"
  | "[=" ~ date ~ "]"
}

lot_note = {
    "(" ~ ws* ~ lot_note_text ~ ws* ~ ")"
}

lot_note_text = {
    (!("(" | ")") ~ any_on_line)+
}

cost = {
    cost_detail ~ ws+ ~ quantity
}

cost_detail = {
    "@@"
  | "@"
}

date = {
    int4 ~ date_sep ~ int2 ~ date_sep ~ int2
}

int4 = {
    ASCII_DIGIT{4}
}

int2 = {
    ASCII_DIGIT{2}
}

date_sep = _{
    "/"
  | "-"
  | "."
}

comment_text = @{
    text?
}

text = _{
    word ~ (ws+ ~ word)*
}

word = _{
    (!(eol | ws) ~ ANY)+
}

ws = _{
    " "
  | "\t"
}

any_on_line = _{
    !(eol) ~ ANY
}

any_print = _{
    !(eol | ws) ~ ANY
}


blank_line = _{
    ws* ~ eol
}

eol = _{
    NEWLINE
}

commet_char = _{
    ";"+
}

code_text = {
    (!("(" | ")") ~ any_on_line)+
}

ammount = @{
    "-"? ~ integer ~ ("." ~ integer)?
}

integer = @{
    ASCII_DIGIT{1, }
}
