journal = {
    SOI ~ blank_line* ~ element_list ~ blank_line* ~ EOI
}

element_list = {
    element? ~ (blank_line* ~ element)*
}

element = _{
    xact | journal_comment
}

xact = {
    xact_date ~ (ws+ ~ state)? ~ (ws+ ~ code)? ~ ws+ ~ payee ~ ((sep_ws ~ comment) | ((ws* ~ eol) ~ (ws+ ~ comment)?)) ~ postings

}

xact_date = {
    tx_date ~ ef_date?
}

tx_date = {
    date
}

ef_date = {
    "=" ~ date
}

state = {
    "*"
  | "!"
}

code = {
    "(" ~ code_text ~ ")"
}

payee = @{
    (!("(" | ")") ~ payee_word) ~ ((!("  ;") ~ ws)+ ~ payee_word)*
}

payee_word = _{
    (!("  ;") ~ any_print)+
}

comment = {
    (xact_comment_char ~ ws* ~ comment_text ~ ws* ~ eol) ~
    (ws+ ~ (xact_comment_char ~ ws* ~ comment_text~ ws* ~ eol))*
}


journal_comment = {
    (journal_comment_char ~ ws* ~ comment_text ~ eol)+ |
    "comment" ~ ws* ~ eol ~ comment_block_text ~ ws*  ~ eol? ~ "end comment" |
    "comment" ~ ws* ~ comment_block_text ~ ws*  ~ "end comment" |
    "comment" ~ ws* ~ eol ~  (ws* ~ comment_block_text ~ ws* ~ eol)* ~ "end comment"

}

comment_block_text = @{
    comment_block_word? ~ (ws+ ~ comment_block_word)*
}

comment_block_word = _{
    (!("end comment" | eol | ws) ~ ANY)+
}

postings = {
    posting+
}

posting = {
    ws+ ~ (state ~ ws*)?  ~ account ~ sep_ws ~ quantity ~ (ws+ ~ lots)? ~ (ws+ ~cost)?  ~ ((sep_ws ~ comment) | ((ws* ~ eol) ~ (ws+ ~ comment)?)) |
    ws+ ~ (state ~ ws*)?  ~ account ~ ((sep_ws ~ comment) | ((ws* ~ eol) ~ (ws+ ~ comment)?))
}

account = {
    account_name
  | "(" ~ account_name ~ ")"  // TODO: this need be fixed
  | "[" ~ account_name ~ "]"  // TODO: this need be fixed
}

account_name = @{
    (!("*" | "!") ~ account_word) ~ (ws ~ account_word)*
}

account_word = @{
    (!(" " | "\t") ~ any_on_line)+
}

quantity = {
    units_value // | units_expr
}

units_value = {
    ammount ~ ws* ~commodity
  | commodity ~ ws* ~ammount
}


commodity = @{
    (!(ws | ASCII_DIGIT  | "." | "," | ";" | ":" | "?" | "!" | "-" | "+" | "*" | "/" | "^" | "&" | "|" | "=" | "{" | "}" | "[" | "]" | "<" | ">" | "(" | ")" | "@") ~ any_on_line)+
  | "\"" ~ text ~ "\""
}

lots = {
    lot_price ~ ws* ~lot_date  ~ ws* ~lot_note
  | lot_price ~ ws* ~lot_note  ~ ws* ~lot_date
  | lot_date  ~ ws* ~lot_price ~ ws* ~lot_note
  | lot_date  ~ ws* ~lot_note  ~ ws* ~lot_price
  | lot_note  ~ ws* ~lot_price ~ ws* ~lot_date
  | lot_note  ~ ws* ~lot_date  ~ ws* ~lot_price
  | lot_price ~ ws* ~lot_date
  | lot_price ~ ws* ~lot_note
  | lot_date  ~ ws* ~lot_price
  | lot_date  ~ ws* ~lot_note
  | lot_note  ~ ws* ~lot_price
  | lot_note  ~ ws* ~lot_date
  | lot_price
  | lot_date
  | lot_note
}

lot_price = {
    "{{" ~ total_point_value ~ "}}"
  | "{"  ~ per_unit_point_value ~ "}"
  | "{=" ~ fixing_value ~ "}"
}

total_point_value = {
    units_value
}

per_unit_point_value = {
    units_value
}

fixing_value = {
    units_value
}

lot_date = {
    "[" ~ date ~ "]"
  | "[=" ~ date ~ "]"
}

lot_note = {
    "(" ~ ws* ~ lot_note_text ~ ws* ~ ")"
}

lot_note_text = {
    (!("(" | ")") ~ any_on_line)+
}

cost = {
    cost_detail ~ ws+ ~ quantity
}

cost_detail = {
    "@@"
  | "@"
}

date = {
    int4 ~ date_sep ~ int2 ~ date_sep ~ int2
}

int4 = {
    ASCII_DIGIT{4}
}

int2 = {
    ASCII_DIGIT{2}
}

date_sep = _{
    "/"
  | "-"
  | "."
}

comment_text = @{
    text?
}

text = _{
    word ~ (ws+ ~ word)*
}

word = _{
    (!(eol | ws) ~ ANY)+
}

ws = _{
    " "
  | "\t"
}

sep_ws = _{
    " "{2,}
  | "\t"{1,}
  | (" " | "\t"){2,}
}

any_on_line = _{
    !(eol) ~ ANY
}

any_print = _{
    !(eol | ws) ~ ANY
}


blank_line = _{
    ws* ~ eol
}

eol = _{
    NEWLINE
}

xact_comment_char = _{
    ";"+
}

journal_comment_char = _{
    ";"+ | "#"+ | "%"+ | "|"+ | "*"+
}



code_text = {
    (!("(" | ")") ~ any_on_line)+
}

ammount = @{
    "-"? ~ integer ~ ("." ~ integer)?
}

integer = @{
    ASCII_DIGIT{1, }
}
